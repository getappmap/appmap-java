buildscript {
	ext {
		if (JavaVersion.current() >= JavaVersion.VERSION_25) {
			springBootVersion = '3.1.4'
			javaVersion = JavaVersion.VERSION_17
		} else {
			springBootVersion = '2.7.0'
			javaVersion = JavaVersion.VERSION_1_8
		}
	}
}

plugins {
		id 'org.springframework.boot' version "${springBootVersion}"
		id 'io.spring.dependency-management' version '1.1.7'
		id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.oracle.database.jdbc:ojdbc8:21.9.0.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

def appmapJar = "$System.env.AGENT_JAR"

// Spring boot 3.x uses Jakarta EE namespaces
task transformSources(type: Copy) {
	from 'src/main/java'
	into "$buildDir/generated-sources/transformed"

	if (springBootVersion.startsWith("3.")) {
		filter { line ->
			line.replaceAll('javax.persistence', 'jakarta.persistence')
		}
	}
}

sourceSets.main.java.srcDirs = [transformSources.destinationDir]
compileJava.dependsOn transformSources

test {
	useJUnitPlatform()
	if (System.env.ORACLE_URL) {
		inputs.property("oracleUrl", System.env.ORACLE_URL)
	}
	if (System.env.AGENT_JAR) {
		inputs.file(System.env.AGENT_JAR)
	}
	jvmArgs += [
		"-javaagent:${appmapJar}",
		"-Dappmap.config.file=appmap.yml",
		"-Djava.util.logging.config.file=${System.env.JUL_CONFIG}",
		// "-Dappmap.debug=true",
	]
}
